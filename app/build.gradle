apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 28
        versionName '1.2'
        archivesBaseName = "$versionName-$versionCode"
    }

    // conditionally load keystore.properties and configure release signing config
    File keystorePropertiesFile = new File("keystores.properties")
    if (keystorePropertiesFile.exists()) {
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            versionNameSuffix '.debug'
            zipAlignEnabled true
        }
        release {
            debuggable false
            zipAlignEnabled true
        }
    }
}

dependencies {
    // Support
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:preference-v7:27.1.1'
    implementation 'com.android.support:preference-v14:27.1.1'
    // UI
    implementation files('libs/PinnedHeaderListView.jar')
    implementation 'com.melnykov:floatingactionbutton:1.3.0'
    implementation 'com.jpardogo.materialtabstrip:library:1.1.0'
    implementation('com.github.afollestad.material-dialogs:core:0.8.5.2@aar') { transitive = true }
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.jakewharton:butterknife:7.0.1'
    annotationProcessor 'com.jakewharton:butterknife:7.0.1'
    implementation 'com.android.support:design:27.1.1'
    // Database
    implementation 'com.github.satyan:sugar:b16e65326a'
    // Networking
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.loopj.android:android-async-http:1.4.8'
    implementation 'org.apache.httpcomponents:httpcore:4.3.2'
    // IO
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'commons-io:commons-io:2.4'
    // Pre-processing
    implementation 'com.hannesdorfmann.fragmentargs:annotation:2.1.2'
    implementation 'com.hannesdorfmann.fragmentargs:bundler-parceler:2.1.2'
    annotationProcessor 'com.hannesdorfmann.fragmentargs:processor:2.1.2'
    implementation "org.parceler:parceler-api:1.0.3"
    annotationProcessor "org.parceler:parceler:1.0.3"
    implementation 'com.github.hotchemi:permissionsdispatcher:2.1.3'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.1.3'
    // Debug
    implementation 'com.facebook.stetho:stetho:1.3.1'
}
